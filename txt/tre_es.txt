import:--import	 tre_es.txt
	SET QUOTED_IDENTIFIER OFF
	declare @cmd nvarchar(max)
	declare @t_key nvarchar(10) = [1] --'BJ'
	declare @t_datea nvarchar(10) = [2] --'105/09/21'
	declare @t_bdate nvarchar(20) = [3] --'105/08/01'
	declare @t_edate nvarchar(20) = [4] --'105/09/30'
	declare @t_carno nvarchar(max) = [5] --''
	declare @t_driverno nvarchar(max) = [6] --''
	----------------------------------------------------------------------------------------
	----------------------------------------------------------------------------------------
	declare @tmp table(
		sel int identity(1,1)
		,tranaccy nvarchar(10)
		,tranno nvarchar(20)
		,trannoq nvarchar(10)
		
		,comp nvarchar(50)
		
		,datea nvarchar(20)
		,trandate nvarchar(20)
		,straddrno nvarchar(20)
		,straddr nvarchar(50)
		
		,carno nvarchar(20)
		,driverno nvarchar(20)
		,driver nvarchar(50)
		
		,inmount float
		,pton float
		,mount float
		,price float
		,price2 float
		,price3 float
		,discount float
		,reserve float
		,[weight] float
		,outmount float
		,pton2 float
		,mount2 float		
		,total float
		,total2 float
		,[status] nvarchar(20)
		,mount3 float
		,mount4 float	
		
		,aaddr nvarchar(max)
	)
	insert into @tmp(tranaccy,tranno,trannoq,datea,trandate,comp,straddrno,straddr,carno,driverno,driver
		,inmount,pton,mount,price,price2,price3,discount,reserve,[weight],outmount,pton2,mount2
		,total,total2,[status],mount3,mount4,aaddr)
	select a.accy,a.noa,a.noq,a.datea,a.trandate,a.comp,a.straddrno,a.straddr,a.carno,isnull(a.driverno,''),isnull(a.driver,'')
		,a.inmount,a.pton,a.mount,a.price,a.price2,a.price3,a.discount,a.reserve,a.[weight],a.outmount,a.pton2,a.mount2
		,a.total,a.total2,a.[status],a.mount3,a.mount4,a.aaddr
	from view_trans a
	where isnull(a.trandate,'') between @t_bdate and @t_edate
	and (len(@t_carno)=0 or CHARINDEX(','+a.carno+',',','+@t_carno+',')>0)
	and (len(@t_driverno)=0 or CHARINDEX(','+a.driverno+',',','+@t_driverno+',')>0)
	
	--刪除已匯入過的
	delete @tmp 
	from @tmp a
	outer apply (select noa from view_tres where tranno=a.tranno and trannoq=a.trannoq) b
	where b.noa is not null
	---------------------------------------------------------------------------------------------------
	--加減項
	declare @tmpcarchg table(
		sel int identity(1,1)
		,driverno nvarchar(20)
		,noa nvarchar(20)
		,plusmoney float
		,minusmoney float
		,datea nvarchar(10)
	)
	insert into @tmpcarchg(driverno,noa,plusmoney,minusmoney,datea)
	select a.driverno,a.noa,a.plusmoney,a.minusmoney,a.datea
	from carchg a
	outer apply (select top 1 * from view_tre where CHARINDEX(','+a.noa+',',','+carchgno+',')>0) b
	where b.noa is null
	and a.datea between @t_bdate and @t_edate
	---------------------------------------------------------------------------------------------------
	IF OBJECT_ID('tempdb..#tre_es')is not null
	BEGIN
		drop table #tre_es
	END
	create table #tre_es (
		sel int identity(1,1)
		,noa nvarchar(20)
		,datea nvarchar(20)
		,driverno nvarchar(20)
		,driver nvarchar(20)
		,carchgno nvarchar(max)
		,[money] float
		,plusmoney float
		,minusmoney float
		,total float
		,memo nvarchar(max)
	)
	
	IF OBJECT_ID('tempdb..#tres_es')is not null
	BEGIN
		drop table #tres_es
	END
	create table #tres_es (
		sel int identity(1,1)
		,noa nvarchar(20)
		,noq nvarchar(10)
		,trandate nvarchar(20)
		,comp nvarchar(50)
		,carno nvarchar(20)
		,driverno nvarchar(20)
		,driver nvarchar(20)
		,addrno nvarchar(20)
		,addr nvarchar(50)
		,price float
		,discount float
		,[money] float
		
		,tranaccy nvarchar(10)
		,tranno nvarchar(20)
		,trannoq nvarchar(10)
	)
	insert into #tres_es(noa,noq,trandate,comp,carno,driverno,driver,addrno,addr,price,discount,[money],tranaccy,tranno,trannoq)
	select '' noa,''noq,trandate,comp,carno,driverno,driver,''straddrno,aaddr,price2,discount,total2,tranaccy,tranno,trannoq
	from @tmp
	
	-----------------------------------------------------------------------------------------------
	declare @string nvarchar(max) = '0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'
	
	declare @driverno nvarchar(max)
	declare @n int
	declare @carchgno nvarchar(max) = ''
	
	declare @noa nvarchar(20)
	declare @plus float
	declare @minus float
	declare @plusmoney float
	declare @minusmoney float
	declare @money float
	declare @total float
	
	declare @maxno1 nvarchar(20)
	declare @maxno2 nvarchar(20)
	
	declare cursor_table cursor for
	select driverno
	from(select driverno from @tmp 
	union all 
	select driverno from @tmpcarchg) a
	group by driverno
	open cursor_table
	fetch next from cursor_table
	into @driverno
	while(@@FETCH_STATUS <> -1)
	begin
		select @carchgno = '',@plusmoney=0,@minusmoney=0,@money=0,@total=0
		
		declare cursor_table2 cursor for
		select a.noa,a.plusmoney,a.minusmoney
		from @tmpcarchg a
		where a.driverno=@driverno
		open cursor_table2
		fetch next from cursor_table2
		into @noa,@plus,@minus
		while(@@FETCH_STATUS <> -1)
		begin
			select @carchgno = case when len(@carchgno)>0 then ',' else '' end+@noa
				,@plusmoney=@plusmoney+ISNULL(@plus,0)
				,@minusmoney=@minusmoney+ISNULL(@minus,0)
			fetch next from cursor_table2
			into @noa,@plus,@minus
		end
		close cursor_table2
		deallocate cursor_table2	
	
		select @money=SUM(ISNULL([money],0)) from #tres_es where driverno=@driverno
		select @total = isnull(@money,0) + isnull(@plusmoney,0) - isnull(@minusmoney,0)
		-----------------------------------------------------
		select @noa='',@maxno1='',@maxno2=''
		select top 1 @maxno1 = noa from view_tre where noa like @t_key+REPLACE(@t_datea,'/','')+'[0-9,A-Z][0-9][0-9]' order by noa desc
		select top 1 @maxno2 = noa from #tre_es where noa like @t_key+REPLACE(@t_datea,'/','')+'[0-9,A-Z][0-9][0-9]' order by noa desc

		set @noa = case when @maxno1>@maxno2 then @maxno1 else @maxno2 end
		set @noa = case when len(@noa)=0 then @t_key+REPLACE(@t_datea,'/','')+'000' else @noa end

		set @n = (charindex(left(RIGHT(@noa,3),1),@string)-1)*100 + cast(RIGHT(@noa,2) as int) + 1
		set @noa = @t_key+REPLACE(@t_datea,'/','')+SUBSTRING(@string, floor(@n/100)+1,1) + right('00'+cast(@n%100 as nvarchar),2)
		
		insert into #tre_es(noa,datea,driverno,carchgno,[money],plusmoney,minusmoney,total,memo)
		select @noa,@t_datea,@driverno,@carchgno,@money,@plusmoney,@minusmoney,@total,''
		
		update #tres_es set noa=@noa where driverno=@driverno
		
		fetch next from cursor_table
		into @driverno
	end
	close cursor_table
	deallocate cursor_table	
	
	update #tre_es set driver=ISNULL(b.namea,'')
	from #tre_es a
	left join driver b on a.driverno=b.noa
	
	update #tres_es set noq= RIGHT('00'+cast(b.recno as nvarchar),3)
	from #tres_es a
	left join (select sel,ROW_NUMBER()over(partition by noa order by sel) recno from #tres_es ) b on a.sel=b.sel
	
	---------------------------------------------------------------------------------------------------------
	declare @accy nvarchar(10) = left(@t_datea,3)
	--司機資料寫入廠商主檔
	declare @tmptgg table(
		sel int identity(1,1)
		,noa nvarchar(20)
		,comp nvarchar(50)
		,nick nvarchar(20)
		,serial nvarchar(20)
		,tel nvarchar(20)
		,mobile nvarchar(20)
		,zip_home nvarchar(20)
		,addr_home nvarchar(100)
	)
	insert into @tmptgg(noa,comp,nick,serial,tel,mobile,zip_home,addr_home)
	select a.driverno,a.driver,a.driver,b.idno,b.tel,b.mobile,b.zip_home,b.addr_home
	from #tre_es a
	left join driver b on a.driverno=b.noa
	left join tgg c on a.driverno = c.noa
	where c.noa is null
	and len(ISNULL(a.driverno,''))>0
	---------------------------------------------------------------------------------------------------------
	declare @curtime datetime = getdate()
	
	DECLARE @chk tinyint = 0
	Begin Transaction [Trans_Name]

	begin try
		set @cmd =
		"insert into tre"+@accy+"(noa,datea,driverno,driver,tggno,tgg,carchgno,[money],plusmoney,minusmoney,[total],memo)
		select noa,datea,driverno,driver,driverno,driver,carchgno,[money],plusmoney,minusmoney,[total],memo from #tre_es"
		execute sp_executesql @cmd
		
		set @cmd=
		"insert into tres"+@accy+"(noa,noq,trandate,comp,driverno,driver,ship,straddr,price,discount,[money],tranaccy,tranno,trannoq)
		select noa,noq,trandate,comp,driverno,driver,carno,addr,price,discount,[money],tranaccy,tranno,trannoq from #tres_es"
		execute sp_executesql @cmd
		
		insert into tgg(noa,comp,nick,serial,tel,mobile,zip_home,addr_home,memo)
		select noa,comp,nick,serial,tel,mobile,zip_home,addr_home,'司機立帳產生' 
		from @tmptgg
		
		--產生傳票
		INSERT INTO mess (datea,qtime,[tables],data,usera,act) 
		select replace(CONVERT(nvarchar,getdate(),120),'-','')
			,noa+' '+replace(CONVERT(nvarchar,@curtime,120),':','') qtime
			,'tre_post.post' [tables]
			,@accy+','+noa+',1'
			,'erp'
			,7
		from #tre_es
		
		Commit Transaction [Trans_Name] -- 提交所有操作所造成的變更
		select 'done' msg 
	end try
	begin catch
		Rollback Transaction [Trans_Name] -- 復原所有操作所造成的變更
		select ERROR_MESSAGE() msg 
	end catch
	
	drop table #tre_es
	drop table #tres_es;